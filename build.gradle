apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

repositories {
    mavenCentral()
    jcenter()
}

version = '1.0'

dependencies {
    compile 'com.google.guava:guava:20.0'
    compile 'commons-cli:commons-cli:1.4'

    testCompile 'junit:junit:4.12'
}

mainClassName = 'App'

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Encrypt files into images and back again',
                'Implementation-Version': version,
                'Main-Class': 'App'
    }
    baseName = project.name + '-complete'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyJar(type: Copy) {
    from file("${buildDir}/libs/${project.name}-complete-${version}.jar")
    into file("${projectDir}")
}

task superBuild(dependsOn: ['clean', 'build', 'fatJar', 'copyJar']){
    build.mustRunAfter clean
    fatJar.mustRunAfter build
    copyJar.mustRunAfter fatJar
}

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}